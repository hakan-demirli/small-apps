{
  description = "A collection of small applications";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixos-25.11";
    flake-utils.url = "github:numtide/flake-utils";
  };

  outputs =
    {
      nixpkgs,
      flake-utils,
      ...
    }:
    let
      lib = nixpkgs.lib;
      findPackageDirs = fpath: lib.filterAttrs (name: type: type == "directory") (builtins.readDir fpath);
      allPackageNames = lib.attrNames (findPackageDirs ./pkgs);

      perSystem =
        flake-utils.lib.eachSystem
          (lib.filter (s: !(lib.hasInfix "darwin" s)) flake-utils.lib.defaultSystems)
          (
            system:
            let
              pkgs = import nixpkgs { inherit system; };

              allMyPackages = lib.genAttrs allPackageNames (
                name: pkgs.callPackage (./pkgs + "/${name}/default.nix") { }
              );
            in
            {
              packages = allMyPackages // {
                default =
                  let
                    enabledPackages = lib.filterAttrs (name: pkg: !(pkg.meta.broken or false)) allMyPackages;
                  in
                  pkgs.buildEnv {
                    name = "small-apps-bundle-${system}";
                    paths = lib.attrValues enabledPackages;
                    meta.description = "Build environment containing all enabled small-apps";
                  };
              };
            }
          );

    in
    perSystem
    // {
      overlays.default =
        final: prev:
        let
          system = prev.system;
          systemPackages = perSystem.packages.${system} or { };
          packagesToAdd = lib.filterAttrs (name: _: name != "default") systemPackages;
        in
        packagesToAdd;
    };
}
